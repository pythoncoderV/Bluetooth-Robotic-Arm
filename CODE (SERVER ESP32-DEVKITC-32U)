CODE:

This code uses Bluetooth to wirelessly control a 4-servo robotic arm. Four channels (servos 0-3) are switched by the joystick button. 
After the fourth servo, the button is pressed to return to the first. The position of the active servo is gradually changed by moving the 
joystick to the left or right. The angle is increased when pushing right and decreased when pushing left. The servo does not return to the 
starting position when the joystick reaches the center; instead, it maintains its previous position. Various movement speeds are used by 
the system. While servo 3 (the SG90 gripper) moves quickly (speed = 8) for quick grabbing, servos 0, 1, and 2 move slowly for accuracy 
(speed = 2). To keep it from rotating too much, each servo has position limits. The gripper uses a smaller 102-512 range for improved grip
control, while servos 0-2 operate within a 150-600 pulse range. Through Bluetooth, the ESP32 continuously receives joystick data from the 
master controller. After identifying the active channel, it applies the proper speed and limits, computes the new position using the joystick's 
direction, and then transmits PWM signals to the PCA9685 driver board. The chosen servo is physically moved by this board.

________________________________________________________________________________________________________________________________________________

#include "BluetoothSerial.h"
#include <Wire.h>
#include <Adafruit_PWMServoDriver.h>


BluetoothSerial SerialBT;
Adafruit_PWMServoDriver pwm = Adafruit_PWMServoDriver();


int currentServo = 0;
int VRx = 0;
int SW = 0;


//Range for Servos
int servoMin = 150;
int servoMax = 600;
int servoPos[4] = {375, 375, 375, 375};


// Gripper custom range to have a tighter grip (Channel 3 - SG90)
int gripperMin = 102;
int gripperMax = 512;


// Speed
int moveSpeed = 2;    // Servos 0, 1, 2 are slow/normal
int sg90Speed = 8;    // Servo SG90 gripper is fast


bool lastButtonState = HIGH;


String masterName = "RobotMaster";


void setup() {
  Serial.begin(115200);
 
 
  Wire.begin(21, 22);
 
    pwm.begin();
  pwm.setPWMFreq(60);
 
 
  SerialBT.begin("RobotSlave", true);
 
  Serial.println("4-Servo Robot Arm Ready");
  Serial.println("Connecting to master...");
 
  delay(1000);
 
  bool connected = SerialBT.connect(masterName);
  if (connected) {
    Serial.println("✓ Connected!");
  } else {
    Serial.println("✗ Connection failed");
  }
}


void loop() {


  if (!SerialBT.connected()) {
    Serial.println("Reconnecting...");
    SerialBT.connect(masterName);
    delay(2000);
    return;
  }
 
  // Receive joystick data
  if (SerialBT.available()) {
    String data = SerialBT.readStringUntil('\n');
    int commaIndex = data.indexOf(',');
   
    if (commaIndex > 0) {
      VRx = data.substring(0, commaIndex).toInt();
      SW = data.substring(commaIndex + 1).toInt();
    }
  }
 
  // Button channel switcher
  bool buttonState = (SW == LOW);
  if (buttonState == LOW && lastButtonState == HIGH) {
    currentServo++;
    if (currentServo > 3) currentServo = 0;
    Serial.print("Channel: ");
    Serial.println(currentServo + 1);
    delay(250);
  }
  lastButtonState = buttonState;
 
  // Joystick control
  int xVal = VRx;
  int center = 512;
 
  if (abs(xVal - center) > 100) {
    
    int speed;
    if (currentServo == 3) {
      speed = sg90Speed;  
    } else {
      speed = moveSpeed;
    }
   
    // Move servo based on joystick
    if (xVal > center + 100) {
      servoPos[currentServo] += speed;
    } else if (xVal < center - 100) {
      servoPos[currentServo] -= speed;
    }
   
    // Apply limits
    if (currentServo == 3) {
      
      servoPos[currentServo] = constrain(servoPos[currentServo], gripperMin, gripperMax);
    } else {
      
      servoPos[currentServo] = constrain(servoPos[currentServo], servoMin, servoMax);
    }
   
    
    pwm.setPWM(currentServo, 0, servoPos[currentServo]);
    delay(15);
  }
}
